import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.guardsquare:proguard-gradle:7.4.2"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "2.0.0-Beta3"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "tokyo.caimingyu"
version = "Version 1.0.0 (2024.02.04)"

repositories {
    google()
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

kotlin {
    jvmToolchain(21)
}

dependencies {}

tasks {
    shadowJar {
        archiveFileName = "hydragon.jar"
        manifest {
            attributes "Main-Class": "tokyo.caimingyu.hydragon.MainKt"
        }
    }
}

tasks.register("proguard", ProGuardTask) {
    configuration file("proguard.pro")
    injars(tasks.named("shadowJar", Jar).flatMap { it.archiveFile })
    if (System.getProperty("java.version").startsWith("1.")) {
        libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    } else {
        libraryjars "${System.getProperty("java.home")}/jmods/java.base.jmod", jarfilter: "!**.jar", filter: "!module-info.class"
    }
    outjars(layout.buildDirectory.file("libs/hydragon-minsize.jar"))
}